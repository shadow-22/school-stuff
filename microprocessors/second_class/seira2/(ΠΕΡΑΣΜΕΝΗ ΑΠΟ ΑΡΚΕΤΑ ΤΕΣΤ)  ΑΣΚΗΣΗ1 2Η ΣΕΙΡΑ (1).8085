ORG 0800H
IN 10H ; MNHMH 3EKLEIDWMA
MVI A,0DH ; / ENERGOPOIHSH DIAKOPHS RST6.5
SIM
EI        ;/
LXI H,0900H ; 8ESH MNHMHS 
MVI M,00H   ; ARXIKOPOIHSH MNHMHS/METRHTH DIAKOPWN

LXI B,01F4H  ; EPI8YMHTH XRONOKA8YSTERHSH THS DELB
MVI D,01H    ; TO BAGONI KINEITE ARXIKA LSB->MSB ME ARXH TO LSB E3ODOU
JMP DRIVE 
CLUTCH2:     ; PRO8ALAMOS ALLAGHS APO REVERSE SE DRIVE
MVI M,00H    ;  EPANARXIKOPOIHSH TOU METRHTH DIAKOPWN
MOV A,D      ;/ GIA AMESH APOKRISH TOU VAGONIOU STHN ALLAGH
RLC
MOV D,A      ; /

DRIVE:      ; GIA KINHSH LSB->MSB(MPROSTA)
LDA 2000H   ; ELEGXOS LSB THS EISODOU 20H
RRC        
JNC SHUT    ; AN LSB(20H)=0, ADRANOPOIHSH DIAKOPWN
MOV E,M     ; MNHMH TWN PATHMATWN DIAKOPWN
MOV A,M     ;/ELEGXOS GIA 1 PATHMA DIAKOPHS
CPI 02H
JZ STOP     ;/ AN EXOUME APLO PATHMA PHGAINE STO STOP 
MOV A,M     ;/ ELEGXOS GIA DIPLO PATHMA DIAKOPHS
CPI 04H
JZ CLUTCH   ;/ AN EXOUME 2 PATHMATA DIAKOPHS PHGAINE SE PRO8ALAMOS ALLAGHS FORAS KINHSHS APO DRIVE SE REVERSE
MOV A,D     ;ELEGXOS AN FTASAME STO MSB(ARISTERO AKRO)
CPI 80H
JZ STOP     ;AN NAI STOP
MOV A,D     ;/E3ODOS-KINHSH VAGONIOU
CMA         
STA 3000H   
CALL DELB   ;ME KATALLHLH KA8YSTERHSH 0,5SEC
MOV A,D     ;/ KINHSH ARISTERA
RLC
MOV D,A     
JMP DRIVE   ;/ 

STOP:       ; STAMATHMA-PAUSH THS KINHSHS
MOV A,D
CMA 
STA 3000H
JMP DRIVE   ; EPANELEGXOS GIA ALLO INPUT

CLUTCH:     ; PRO8ALAMOS ALLAGHS FORAS KINHSHS APO DRIVE SE REVERSE
MVI M,00H   ; EPANARXIKOPOIHSH TOU METRHTH DIAKOPWN
MOV A,D     ;/ GIA AMESH ALLAGH FORAS
RRC
MOV D,A     ;/

REVERSE:    ; /PAROMOIA DIADIKASIA ME TO DRIVE ME MONH ALLAGH THN FORA THS KINHSHS-OLIS8HSHS TOU VAGONETOU
LDA 2000H
RRC
JNC SHUT1
MOV E,M
MOV A,M
CPI 02H
JZ STOP2
MOV A,M
CPI 04H
JZ CLUTCH2
MOV A,D
CPI 01H
JZ STOP2
MOV A,D
CMA
STA 3000H
CALL DELB
MOV A,D
RRC
MOV D,A
JMP REVERSE
STOP2:
MOV A,D
CMA 
STA 3000H
JMP REVERSE  ;/


INTR_ROUTINE: ;ME TO PATHMA DIAKOPHS KALEITE H ROUTINA DIAKOPWN
INR M         ; AU3HSH TOU METRHTH DIAKOPWN-PATHMATWN DIAKOPHS
EI
RET
 
SHUT:   ;SE PERIPTWSH GENIKHS EISODOU 0 ADRANOPOIHSH DIAKOPWN SE SYNDYASMO ME DIATHRHSH THS MNHMHS TWN PATHMATWN
MOV M,E ; GIA ALLH SYMPERIFORA OSON AFORA THN MNHMH DIAKOPWN SE METAVATIKH FASH TOU LSB VAZW MVI M,00H KAI DN XRHSIMOPOIW TON E
JMP DRIVE
SHUT1:   ; PAROMOIA DIADIKASIA ME SHUT GIA THN PERIPTWSH THS REVERSE KINHSHS
MOV M,E
JMP REVERSE

; !!! TO PROGRAMMA DN EINAI SXEDIASMENO NA LEITOURGEI ME SYNEXOMENA POLY GRHGORA PATHMATA TOU INTRPT PLHKTROU !!!

END